//used to fetch package.json file
import groovy.json.JsonSlurper

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.2.2"
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    maven {
        url "https://android-sdk.voxeet.com/release"
        content {
            includeGroup "com.voxeet.sdk"
        }
    }
    maven {
        url "https://android-sdk.voxeet.com/beta"
        content {
            includeGroup "com.voxeet.sdk"
        }
    }
}

apply plugin: 'com.android.library'

def ext = rootProject.ext
def _compile = ext.has("compileSdkVersion") ? ext.compileSdkVersion : 33
def _buildTools = ext.has("buildToolsVersion") ? ext.buildToolsVersion : "33.0.0"
def _minSdk = ext.has("minSdkVersion") ? ext.minSdkVersion : 21
def _targetSdk = ext.has("targetSdkVersion") ? ext.targetSdkVersion : 33

if(_minSdk < 21) _minSdk = 21

def packageJson = new JsonSlurper().parse file("../package.json")
def version = packageJson.version

android {
    compileSdkVersion _compile
    buildToolsVersion _buildTools

    defaultConfig {
        minSdkVersion _minSdk
        targetSdkVersion _targetSdk
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "VOXEET_REACT_NATIVE_VERSION", "\"${version}\""
    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.annotation:annotation:1.1.0"

    //add by default the optional firebase dependency
    api ("io.dolby:uxkit-firebase:3.7.5") {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }

    //use the latest uxkit
    api ('io.dolby:uxkit:3.7.5') {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }
    api ('io.dolby:uxkit-system-service:3.7.5') {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }
}
